//
// WARNING!!! PSRAM IC required for UXGA resolution and high JPEG quality
//            Ensure ESP32 Wrover Module or other board with PSRAM is selected
//            Partial images will be transmitted if image exceeds buffer size
//
//            You must select partition scheme from the board menu that has at least 3MB APP space.
//            Face Recognition is DISABLED for ESP32 and ESP32-S2, because it takes up from 15
//            seconds to process single frame. Face Detection is ENABLED if PSRAM is enabled as well

#include "esp_camera.h"       // Include library for camera
#include <WiFi.h>             // Include library for Wi-Fi
#include "esp_timer.h"        // Include library for timer
#include "img_converters.h"   // Include library for image conversion
#include "Arduino.h"          // Include library for Arduino
#include "fb_gfx.h"           // Include library for graphics
#include "soc/soc.h"          // Include library for SoC and disable brownout problems
#include "soc/rtc_cntl_reg.h" // Include library for RTC control register and disable brownout problems
#include "esp_http_server.h"  // Include library for HTTP server

// Set the Wi-Fi credentials *Note: Uptade with your network credentials*
#include "wifi_credentials.h"

// Set the boundary for the multipart message
#define PART_BOUNDARY "123456789000000000000987654321"

// Select camera model
#define CAMERA_MODEL_AI_THINKER // Has PSRAM 
#include "camera_pins.h" // Include library with camera pins

// Function to start the camera server
static const char* _STREAM_CONTENT_TYPE = "multipart/x-mixed-replace;boundary=" PART_BOUNDARY; // Set the content type for the stream
static const char* _STREAM_BOUNDARY = "\r\n--" PART_BOUNDARY "\r\n";                           // Set the boundary for the stream
static const char* _STREAM_PART = "Content-Type: image/jpeg\r\nContent-Length: %u\r\n\r\n";    // Set the part for the stream

// Function to start the camera server
httpd_handle_t stream_httpd = NULL; // Set the stream handle to NULL


/**
 * @brief Handles the streaming of camera frames over HTTP.
 *
 * This function is responsible for capturing camera frames, converting them to JPEG format if necessary,
 * and sending the frames as chunks of data over an HTTP response.
 *
 * @param req Pointer to the HTTP request structure.
 * @return esp_err_t Returns ESP_OK if the streaming is successful, otherwise returns an error code.
 */
static esp_err_t stream_handler(httpd_req_t *req){
  camera_fb_t * fb = NULL;
  esp_err_t res = ESP_OK;
  size_t _jpg_buf_len = 0;
  uint8_t * _jpg_buf = NULL;
  char * part_buf[64];

  // Set the content type for the stream
  res = httpd_resp_set_type(req, _STREAM_CONTENT_TYPE);
  if(res != ESP_OK){
    return res;
  }

  // Loop to capture the frame
  while(true){
    fb = esp_camera_fb_get();
    if (!fb) {
      Serial.println("Camera capture failed");
      res = ESP_FAIL;
    } else {
      if(fb->width > 400){
        if(fb->format != PIXFORMAT_JPEG){
          bool jpeg_converted = frame2jpg(fb, 80, &_jpg_buf, &_jpg_buf_len);
          esp_camera_fb_return(fb);
          fb = NULL;
          if(!jpeg_converted){
            Serial.println("JPEG compression failed");
            res = ESP_FAIL;
          }
        } else {
          _jpg_buf_len = fb->len;
          _jpg_buf = fb->buf;
        }
      }
    }
    if(res == ESP_OK){
      size_t hlen = snprintf((char *)part_buf, 64, _STREAM_PART, _jpg_buf_len);
      res = httpd_resp_send_chunk(req, (const char *)part_buf, hlen);
    }
    if(res == ESP_OK){
      res = httpd_resp_send_chunk(req, (const char *)_jpg_buf, _jpg_buf_len);
    }
    if(res == ESP_OK){
      res = httpd_resp_send_chunk(req, _STREAM_BOUNDARY, strlen(_STREAM_BOUNDARY));
    }
    if(fb){
      esp_camera_fb_return(fb);
      fb = NULL;
      _jpg_buf = NULL;
    } else if(_jpg_buf){
      free(_jpg_buf);
      _jpg_buf = NULL;
    }
    if(res != ESP_OK){
      break;
    }
    //Serial.printf("MJPG: %uB\n",(uint32_t)(_jpg_buf_len));
  }
  return res;
}


/**
 * @brief Starts the camera server.
 * 
 * This function initializes the camera server by configuring the server port and registering the URI handler.
 * The server port is set to 80 by default. The URI handler is responsible for handling HTTP GET requests to the root URI ("/").
 * 
 * @note This function assumes that the `stream_handler` function is defined elsewhere in the code.
 */
void startCameraServer(){
  httpd_config_t config = HTTPD_DEFAULT_CONFIG(); // Set the default configuration for the HTTP server
  config.server_port = 80;                        // Set the server port to 80

  httpd_uri_t index_uri = {
    .uri       = "/",
    .method    = HTTP_GET,
    .handler   = stream_handler,
    .user_ctx  = NULL
  };                                              // Set the URI handler for the HTTP server
  
  //Serial.printf("Starting web server on port: '%d'\n", config.server_port);
  Serial.printf("Open http://esp32cam.local:%d in your browser\n", config.server_port);
  if (httpd_start(&stream_httpd, &config) == ESP_OK) {
    httpd_register_uri_handler(stream_httpd, &index_uri); // Register the URI handler for the HTTP server
  }
}

void setup() {
  WRITE_PERI_REG(RTC_CNTL_BROWN_OUT_REG, 0); //disable brownout detector
 
  Serial.begin(115200);
  Serial.setDebugOutput(false);
  
  camera_config_t config;
  config.ledc_channel = LEDC_CHANNEL_0;
  config.ledc_timer = LEDC_TIMER_0;
  config.pin_d0 = Y2_GPIO_NUM;
  config.pin_d1 = Y3_GPIO_NUM;
  config.pin_d2 = Y4_GPIO_NUM;
  config.pin_d3 = Y5_GPIO_NUM;
  config.pin_d4 = Y6_GPIO_NUM;
  config.pin_d5 = Y7_GPIO_NUM;
  config.pin_d6 = Y8_GPIO_NUM;
  config.pin_d7 = Y9_GPIO_NUM;
  config.pin_xclk = XCLK_GPIO_NUM;
  config.pin_pclk = PCLK_GPIO_NUM;
  config.pin_vsync = VSYNC_GPIO_NUM;
  config.pin_href = HREF_GPIO_NUM;
  config.pin_sscb_sda = SIOD_GPIO_NUM;
  config.pin_sscb_scl = SIOC_GPIO_NUM;
  config.pin_pwdn = PWDN_GPIO_NUM;
  config.pin_reset = RESET_GPIO_NUM;
  config.xclk_freq_hz = 20000000;
  config.pixel_format = PIXFORMAT_JPEG; // Set the pixel format to JPEG
  //config.grab_mode = CAMERA_GRAB_WHEN_EMPTY;
  //config.fb_location = CAMERA_FB_IN_PSRAM;
  if(psramFound()){
    config.frame_size = FRAMESIZE_UXGA; // Set the frame size to UXGA
    config.jpeg_quality = 10;           // Set the JPEG quality to 10
    config.fb_count = 2;                // Set the frame buffer count to 2
  } else {
    config.frame_size = FRAMESIZE_SVGA; // Set the frame size to SVGA
    config.jpeg_quality = 12;           // Set the JPEG quality to 12
    config.fb_count = 1;                // Set the frame buffer count to 1
  }
  
  // Camera init
  esp_err_t err = esp_camera_init(&config);
  if (err != ESP_OK) {
    Serial.printf("Camera init failed with error 0x%x", err);
    return;
  }
  // Wi-Fi connection
  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("");
  Serial.println("WiFi connected");
  
  Serial.print("Camera Stream Ready! Go to: http://");
  Serial.print(WiFi.localIP());
  
  // Start streaming web server
  startCameraServer();
}

void loop() {
  delay(1);
}